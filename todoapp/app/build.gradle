apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.example.android.architecture.blueprints.todomvvmlive"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    flavorDimensions "default"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "default"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.flavors[0].name == 'mock') {
            variant.ignore = true
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    dataBinding {
        enabled = true
    }
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {
    // App's dependencies, including test
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appcompatVersion"
    implementation "androidx.cardview:cardview:$rootProject.ext.cardviewVersion"
    implementation "com.google.android.material:material:$rootProject.ext.materialVersion"
    implementation "androidx.recyclerview:recyclerview:$rootProject.ext.recyclerviewVersion"
    implementation "androidx.legacy:legacy-support-v4:1.0.0-alpha3"
    implementation "androidx.room:room-runtime:$rootProject.ext.roomRuntimeVersion"
    kapt "androidx.room:room-compiler:$rootProject.ext.roomCompilerVersion"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.ext.lifecycleExtensionsVersion"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$rootProject.ext.lifecycleCompilerVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"

    testImplementation "androidx.arch.core:core-testing:$rootProject.ext.archCoreTestingVersion"
    androidTestImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    // Android Testing Support Library's runner and rules
    androidTestImplementation "androidx.test:runner:$rootProject.ext.testRunnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.testRulesVersion"

    // Dependencies for Android unit tests
    androidTestImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "org.mockito:mockito-android:$rootProject.ext.mockitoVersion"

    // Espresso UI Testing
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoCoreVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.ext.espressoContribVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.ext.espressoIntentsVersion"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$rootProject.ext.espressoIdlingConcurrentVersion"
    implementation "androidx.test.espresso:espresso-idling-resource:$rootProject.ext.espressoIdlingResourceVersion"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "androidx.annotation:annotation:1.0.0-alpha3"
    androidTestImplementation "androidx.legacy:legacy-support-v4:1.0.0-alpha3"
    androidTestImplementation "androidx.recyclerview:recyclerview:1.0.0-alpha3"
    androidTestImplementation "androidx.appcompat:appcompat:1.0.0-alpha3"
    androidTestImplementation "androidx.core:core:1.0.0-alpha3"
    androidTestImplementation "com.google.android.material:material:1.0.0-alpha3"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:3.1.0-alpha3"
    androidTestImplementation "androidx.room:room-testing:2.0.0-alpha1"
    androidTestImplementation "androidx.room:room-runtime:$rootProject.ext.roomRuntimeVersion"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    kapt "androidx.databinding:databinding-compiler:$rootProject.ext.gradlePluginVersion"
}

